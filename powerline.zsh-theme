# FreeAgent puts the powerline style in zsh !

if [[ -z "$POWERLINE_DATE_FORMAT" ]]; then
  POWERLINE_DATE_FORMAT="%D{%Y-%m-%d}"
fi

if [[ -z "$POWERLINE_RIGHT_B" ]]; then
  POWERLINE_RIGHT_B="%D{%H:%M:%S}"
fi

if [[ "$POWERLINE_RIGHT_A" == "mixed" ]]; then
  POWERLINE_RIGHT_A=%(?."$POWERLINE_DATE_FORMAT".%F{red}✘ %?)
elif [[ "$POWERLINE_RIGHT_A" == "exit-status" ]]; then
  POWERLINE_RIGHT_A=%(?.%F{green}✔ %?.%F{red}✘ %?)
elif [[ "$POWERLINE_RIGHT_A" == "date" ]]; then
  POWERLINE_RIGHT_A="$POWERLINE_DATE_FORMAT"
else
  POWERLINE_RIGHT_A=""
fi

if [[ "$POWERLINE_HIDE_USER_NAME" != "true" ]] && [[ "$POWERLINE_HIDE_HOST_NAME" != "true" ]]; then
    POWERLINE_USER_NAME="%n@%M"
elif [[ "$POWERLINE_HIDE_USER_NAME" == "true" ]] && [[ "$POWERLINE_HIDE_HOST_NAME" != "true" ]]; then
    POWERLINE_USER_NAME="@%M"
elif [[ "$POWERLINE_HIDE_USER_NAME" != "true" ]] && [[ "$POWERLINE_HIDE_HOST_NAME" == "true" ]]; then
    POWERLINE_USER_NAME="%n"
fi

POWERLINE_CURRENT_PATH="%d"

if [[ -z "$POWERLINE_FULL_CURRENT_PATH" ]]; then
  POWERLINE_CURRENT_PATH="%1~"
fi

if [[ -z "$POWERLINE_GIT_CLEAN" ]]; then
  POWERLINE_GIT_CLEAN="✔"
fi

if [[ -z "$POWERLINE_GIT_DIRTY" ]]; then
  POWERLINE_GIT_DIRTY="✘"
fi

if [[ -z "$POWERLINE_GIT_ADDED" ]]; then
  POWERLINE_GIT_ADDED="%F{green}✚%F{black}"
fi

if [[ -z "$POWERLINE_GIT_MODIFIED" ]]; then
  POWERLINE_GIT_MODIFIED="%F{blue}✹%F{black}"
fi

if [[ -z "$POWERLINE_GIT_DELETED" ]]; then
  POWERLINE_GIT_DELETED="%F{red}✖%F{black}"
fi

if [[ -z "$POWERLINE_GIT_UNTRACKED" ]]; then
  POWERLINE_GIT_UNTRACKED="%F{yellow}✭%F{black}"
fi

if [[ -z "$POWERLINE_GIT_RENAMED" ]]; then
  POWERLINE_GIT_RENAMED="➜"
fi

if [[ -z "$POWERLINE_GIT_UNMERGED" ]]; then
  POWERLINE_GIT_UNMERGED="═"
fi

if [[ -z "$POWERLINE_GIT_AHEAD" ]]; then
  POWERLINE_GIT_AHEAD="⬆"
fi

if [[ -z "$POWERLINE_GIT_BEHIND" ]]; then
  POWERLINE_GIT_BEHIND="⬇"
fi

if [[ -z "$POWERLINE_GIT_DIVERGED" ]]; then
  POWERLINE_GIT_DIVERGED="⬍"
fi

ZSH_THEME_GIT_PROMPT_PREFIX=" \ue0a0 "
ZSH_THEME_GIT_PROMPT_SUFFIX=""
ZSH_THEME_GIT_PROMPT_DIRTY=" $POWERLINE_GIT_DIRTY"
ZSH_THEME_GIT_PROMPT_CLEAN=" $POWERLINE_GIT_CLEAN"

ZSH_THEME_GIT_PROMPT_ADDED=" $POWERLINE_GIT_ADDED"
ZSH_THEME_GIT_PROMPT_MODIFIED=" $POWERLINE_GIT_MODIFIED"
ZSH_THEME_GIT_PROMPT_DELETED=" $POWERLINE_GIT_DELETED"
ZSH_THEME_GIT_PROMPT_UNTRACKED=" $POWERLINE_GIT_UNTRACKED"
ZSH_THEME_GIT_PROMPT_RENAMED=" $POWERLINE_GIT_RENAMED"
ZSH_THEME_GIT_PROMPT_UNMERGED=" $POWERLINE_GIT_UNMERGED"
ZSH_THEME_GIT_PROMPT_AHEAD=" $POWERLINE_GIT_AHEAD"
ZSH_THEME_GIT_PROMPT_BEHIND=" $POWERLINE_GIT_BEHIND"
ZSH_THEME_GIT_PROMPT_DIVERGED=" $POWERLINE_GIT_DIVERGED"

POWERLINE_GIT_INFO_LEFT=""
POWERLINE_GIT_INFO_RIGHT=""

if [[ "$POWERLINE_SHOW_GIT_ON_RIGHT" != "true" ]]; then
  if [[ "$POWERLINE_HIDE_GIT_PROMPT_STATUS" != "true" ]]; then
    POWERLINE_GIT_INFO_LEFT=$'$(git_prompt_info)$(git_prompt_status)'
  else
    POWERLINE_GIT_INFO_LEFT=$'$(git_prompt_info)'
  fi
else
  POWERLINE_GIT_INFO_RIGHT=$'$(git_prompt_info)'
fi

POWERLINE_SEC1_BG=%K{034}
POWERLINE_SEC1_FG=%F{034}
POWERLINE_SEC1_TXT=%F{black}

if [[ $(id -u) -eq 0 ]]; then
  POWERLINE_SEC1_BG=%K{160}
  POWERLINE_SEC1_FG=%F{160}
fi

if [[ "$POWERLINE_DETECT_SSH" == "true" ]]; then
  if [[ -n "$SSH_CLIENT" ]]; then
    POWERLINE_SEC1_BG=%K{160}
    POWERLINE_SEC1_FG=%F{160}
    POWERLINE_SEC1_TXT=%F{white}
  fi
fi

# Left prompt
PROMPT=""

if [[ -n $POWERLINE_USER_NAME ]]; then
  PROMPT="$PROMPT$POWERLINE_SEC1_BG$POWERLINE_SEC1_TXT $POWERLINE_USER_NAME $POWERLINE_SEC1_FG%K{027}"$'\ue0b0'
fi

PROMPT="$PROMPT%K{027}%F{white} $POWERLINE_CURRENT_PATH "

if [[ -n $POWERLINE_GIT_INFO_LEFT ]]; then
  PROMPT="$PROMPT%K{255}%F{027}"$'\ue0b0'"%F{black}$POWERLINE_GIT_INFO_LEFT %k%F{255}"$'\ue0b0'
else
  PROMPT="$PROMPT%k%F{027}"$'\ue0b0'
fi

PROMPT="$PROMPT%k%f "

if [[ "$POWERLINE_NO_BLANK_LINE" != "true" ]]; then
  PROMPT="
$PROMPT"
fi


# Right prompt
if [[ "$POWERLINE_DISABLE_RPROMPT" != "true" ]]; then
  RPROMPT="%k%f"

  if [[ -n "$POWERLINE_RIGHT_A" ]]; then
    RPROMPT="%F{240}"$'\ue0b2'"%K{240}%F{white} $POWERLINE_RIGHT_A $RPROMPT"
  fi

  RPROMPT="%F{255}"$'\ue0b2'"%k%F{black}%K{255} $POWERLINE_RIGHT_B $RPROMPT"

  if [[ -n $POWERLINE_GIT_INFO_RIGHT ]]; then
    RPROMPT="%F{255}"$'\ue0b2'"%F{black}%K{255} $POWERLINE_GIT_INFO_RIGHT $RPROMPT"
  fi
fi
